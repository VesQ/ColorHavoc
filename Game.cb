//********************************************************************************
// Game.cb
//
// Varsinaisen pelin mekaniikka
//********************************************************************************

// Kokoelma junia varten.
Type TRAINS
    Field obj                // Junan objekti
    Field trainColor As Byte // V‰ri
    Field turning            // Onko juna k‰‰ntym‰ss‰, ja jos on, niin mihin suuntaan.
    Field direction          // Mihin suuntaan juna on menossa (jos ei ole k‰‰ntym‰ss‰).
    Field immune As Byte     // Onko juna immuuni tˆrm‰yksille. K‰ytet‰‰n junan luonnissa.
    Field tileX As Short     // Tilekoordinaatti, miss‰ ollaan.
    Field tileY As Short     // Kts. yl‰puolelta.
EndType

// Vakiot junien k‰‰nt‰misille
Const TRAIN_NOTURN    = 0
Const TRAIN_TURNRIGHT = 1
Const TRAIN_TURNLEFT  = 2

// Vakiot junien suunnille
Const TRAINDIR_RIGHT   = 0
Const TRAINDIR_UP      = 1
Const TRAINDIR_LEFT    = 2
Const TRAINDIR_DOWN    = 3

// Vakio, joka kertoo kuinka paljon moninkertainen nopeuden t‰ytyy junilla olla 
// kaarteissa, jotta nemenisiv‰t suoralla menevien kanssa samssa tahdissa. 
// Arvo: PII / 4
Const CURVE_SPEED# = 0.78539816339744830961566084581988
'Const CURVE_SPEED# = 0.8

// Taulukko, joka sis‰lt‰‰ etuk‰teen ladatut kuvat alkupalikoiden kaikista vaiheista.
Dim GAME_PRELOADEDSTARTS( MAPSIZE-1, MAPSIZE-1, 9 )

// Globaali, joka sis‰lt‰‰ boolean-tiedon, vaihdetaanko muokkausmoodista pelimoodiin
// tai toistep‰in.
Global gGameChangeMode
gChangeMode = False

// Globaali, joka sis‰lt‰‰ tiedon, onko peli jo h‰vitty.
Global gGameFailed
gGameFailed = False

// Kuinka paljon ollaan liikuttu yhden ruudun aikana.
// Kun t‰t‰ muuttujaa verrataan TILESIZE-vakioon, saadaan
// tieto, milloin kaikkien junien tulisi olla mennyt yhden
// laatan verran eteenp‰in. T‰m‰ muuttuja pit‰‰ nollata
// aina kun junat ovat kulkeneet yhden ruudun verran.
// Koska junat l‰htev‰t keskelt‰ palasta, on liikutettu
// matka siis puolet yhden laatan koosta.
Global gMovedInTile As Float
gMovedInTile = TILESIZE/2

// Junien siirt‰misen suuruuden m‰‰r‰‰v‰ globaali. FPS-riippumaton.
Global gMovement As Float
gMovement = 0.0

//================================================================================
// Pelin p‰‰silmukka
//================================================================================
Function StartGame()

    // Alustetaan kartta.
    InitializeMap()
    
    // Alustetaan raiteet
    InitializeTracks()
    
    // Muuttuja, joka sis‰lt‰‰ tiedon saako raiteita muokkailla, eli onko junien
    // liikutus k‰ynniss‰.
    allowEditing = True
    
    // Junien liikkeiden nopeuden kerroin
    speedFactor# = 0.05
    
    // Nollataan liikkeet
    gMovedInTile = TILESIZE/2
    gMovement = 0.0
    
    // FPS-riippumaton liike
    oldTime = Timer()
    passedTime = 0
    Repeat
        // Piirret‰‰n taustakuva pelialueelle
        DrawImage GUIIMAGES( GUI_GAMEAREA ), BORDERSIZE, BORDERSIZE
        
        If allowEditing Then
            ClsColor 64,0,0
            
            // Muokataan hiirell‰ raiteita.
            GameEditMap()
            
            // Piirret‰‰n raiteet ruudulle ja tarkistetan samalla hiiren
            // klikkaukset (alla oleva raide p‰‰lle ja raiteiden poisto).
            doubleClicked = MouseDoubleClick()
            For x=0 To MAPSIZE-1
                For y=0 To MAPSIZE-1
                    If TileType(x,y) = TILE_TRACK And MouseOverTile(x,y) Then
                        If doubleClicked Then
                            // Jos tuplaklikattiin ja hiiri on juuri t‰m‰n laatan p‰‰ll‰,
                            // niin vaihdetaan p‰‰ll‰ ja alla olevat laatat kesken‰‰n.
                            // Tarkistetaan kuitenkin ett‰ paikalla on kaksi laattaa ylip‰‰ns‰.
                            If RAILS(x,y,RAIL_UNDER)<>TR_EMPTY Then
                                tmp = RAILS(x,y,RAIL_TOP)
                                RAILS(x,y,RAIL_TOP) = RAILS(x,y,RAIL_UNDER)
                                RAILS(x,y,RAIL_UNDER) = tmp
                            EndIf
                        ElseIf MouseDown(2) Then
                            // Hiiren kakkospainiketta (yleens‰ oikeata) painiketta painettiin.
                            // Poistetaan hiiren alla oleva tile.
                            DeleteTrack(x,y)
                        EndIf
                    EndIf
                    
                    // Piirret‰‰n palanen n‰ytˆlle
                    DrawTrack(x,y)
                Next y
            Next x
        Else
            ClsColor 0,0,0
            
            // Piirret‰‰n raiteet.
            For x=0 To MAPSIZE-1
                For y=0 To MAPSIZE-1
                    DrawTrack( x, y )
                Next y
            Next x
            
            // Asetetaan junien nopeuden suuruus.
            gMovement = speedFactor# * passedTime
            
            // Kuinka paljon ollaan liikuttu yhden framen aikana.
            // Kun t‰t‰ muuttujaa verrataan TILESIZE-vakioon, saadaan
            // tieto, milloin kaikkien junien tulisi olla mennyt yhden
            // laatan verran eteenp‰in. T‰m‰ muuttuja pit‰‰ nollata
            // aina kun junat ovat kulkeneet yhden ruudun verran.
            gMovedInTile = gMovedInTile + gMovement
            
            // Tarkistetaan, ollaanko siirrytty yhden laatan verran.
            If gMovedInTile > TILESIZE Then
                For tr.TRAINS = Each TRAINS
                    // Jos juna oli ‰sken k‰‰ntym‰ss‰, asetetaan sen suunta oikeaksi.
                    If tr\turning <> TRAIN_NOTURN Then
                        If tr\turning = TRAIN_TURNRIGHT Then
                            tr\direction = tr\direction - 1
                        ElseIf tr\turning = TRAIN_TURNLEFT Then
                            tr\direction = tr\direction + 1
                        EndIf
                        
                        If tr\direction >= 4 Then tr\direction = tr\direction - 4
                        If tr\direction < 0 Then tr\direction = tr\direction + 4
                        
                        tr\turning = TRAIN_NOTURN
                    EndIf
                    
                    // Pistet‰‰n juna osoittamaan oikeaan suuntaan, ettei se ala vaeltelemaan.
                    If tr\direction = TRAINDIR_RIGHT Then
                        RotateObject tr\obj, 0
                    ElseIf tr\direction = TRAINDIR_UP Then
                        RotateObject tr\obj, 90
                    ElseIf tr\direction = TRAINDIR_LEFT Then
                        RotateObject tr\obj, 180
                    Else
                        RotateObject tr\obj, 270
                    EndIf
                    
                    // Asetetaan junien tilekoordinaatit koskemaan uutta tile‰.
                    tr\tileX = tr\tileX + ( tr\direction = TRAINDIR_RIGHT ) - ( tr\direction = TRAINDIR_LEFT )
                    tr\tileY = tr\tileY + ( tr\direction = TRAINDIR_DOWN ) - ( tr\direction = TRAINDIR_UP )
                    
                    // Tarkistetaan mink‰ palasen p‰‰ll‰ ollaan nyt.
                    overTile = TileType( tr\tileX, tr\tileY )
                    
                    If overTile = TILE_START Then
                        // Jos oltiin alkupalasen p‰‰ll‰, peli h‰vit‰‰n vain jos juna ei ollut immune.
                        If tr\immune <> True Then
                            GameOver()
                            Exit
                        EndIf
                    ElseIf overTile <> TILE_FINISH Then
                        // Tarkista raiteiden suunta vain, jos ei olla alku/loppupalasen p‰‰ll‰.
                        overTrackTop = RAILS( tr\tileX, tr\tileY, RAIL_TOP )
                        overTrackUnder = RAILS( tr\tileX, tr\tileY, RAIL_UNDER )
                        
                        If overTrackTop = TR_EMPTY Then
                            // Mit‰‰n palasta ei ollut tiless‰.
                            GameOver()
                            Exit
                        Else
                            // P‰‰tell‰‰n, k‰‰nnet‰‰nkˆ junaa oikealle tai vasemmalle, riippuen palasesta
                            // mink‰ p‰‰lle juuri tultiin ja mihin suuntaan juna on t‰ll‰ hetkell‰ menossa.
                            If tr\direction = TRAINDIR_RIGHT Then
                                // Oikealle menon tarkistus
                                If overTrackTop = TR_VERTICAL Or overTrackTop = TR_TOPRIGHT Or overTrackTop = TR_BOTTOMRIGHT Then
                                    checkTrack = overTrackUnder
                                Else
                                    checkTrack = overTrackTop
                                EndIf
                                
                                If checkTrack = TR_BOTTOMLEFT Then
                                    // Asetetaan juna k‰‰ntym‰‰n oikealle.
                                    tr\turning = TRAIN_TURNRIGHT
                                ElseIf checkTrack = TR_TOPLEFT Then
                                    // Asetetaan juna k‰‰ntym‰‰n vasemmalle.
                                    tr\turning = TRAIN_TURNLEFT
                                ElseIf checkTrack = TR_HORIZONTAL Or checkTrack = TR_XCROSS
                                    // Menkˆˆn juna suoraan.
                                    tr\turning = TRAIN_NOTURN
                                Else
                                    // Nyt kyll‰ tˆrm‰t‰‰n.
                                    GameOver()
                                    Exit
                                EndIf
                            ElseIf tr\direction = TRAINDIR_UP Then
                                // Ylˆsp‰in menolle tarkistus
                                If overTrackTop = TR_HORIZONTAL Or overTrackTop = TR_TOPRIGHT Or overTrackTop = TR_TOPLEFT Then
                                    checkTrack = overTrackUnder
                                Else
                                    checkTrack = overTrackTop
                                EndIf
                                
                                If checkTrack = TR_BOTTOMRIGHT Then
                                    // Asetetaan juna k‰‰ntym‰‰n oikealle.
                                    tr\turning = TRAIN_TURNRIGHT
                                ElseIf checkTrack = TR_BOTTOMLEFT Then
                                    // Asetetaan juna k‰‰ntym‰‰n vasemmalle.
                                    tr\turning = TRAIN_TURNLEFT
                                ElseIf checkTrack = TR_VERTICAL Or checkTrack = TR_XCROSS
                                    // Menkˆˆn juna suoraan.
                                    tr\turning = TRAIN_NOTURN
                                Else
                                    // Nyt kyll‰ tˆrm‰t‰‰n.
                                    GameOver()
                                    Exit
                                EndIf
                            ElseIf tr\direction = TRAINDIR_LEFT
                                // Vasemmalle menon tarkistus
                                If overTrackTop = TR_VERTICAL Or overTrackTop = TR_TOPLEFT Or overTrackTop = TR_BOTTOMLEFT Then
                                    checkTrack = overTrackUnder
                                Else
                                    checkTrack = overTrackTop
                                EndIf
                                
                                If checkTrack = TR_TOPRIGHT Then
                                    // Asetetaan juna k‰‰ntym‰‰n oikealle.
                                    tr\turning = TRAIN_TURNRIGHT
                                ElseIf checkTrack = TR_BOTTOMRIGHT Then
                                    // Asetetaan juna k‰‰ntym‰‰n vasemmalle.
                                    tr\turning = TRAIN_TURNLEFT
                                ElseIf checkTrack = TR_HORIZONTAL Or checkTrack = TR_XCROSS
                                    // Menkˆˆn juna suoraan.
                                    tr\turning = TRAIN_NOTURN
                                Else
                                    // Nyt kyll‰ tˆrm‰t‰‰n.
                                    GameOver()
                                    Exit
                                EndIf
                            ElseIf tr\direction = TRAINDIR_DOWN Then
                                // Alasp‰in menolle tarkistus
                                If overTrackTop = TR_HORIZONTAL Or overTrackTop = TR_BOTTOMRIGHT Or overTrackTop = TR_BOTTOMLEFT Then
                                    checkTrack = overTrackUnder
                                Else
                                    checkTrack = overTrackTop
                                EndIf
                                
                                If checkTrack = TR_TOPLEFT Then
                                    // Asetetaan juna k‰‰ntym‰‰n oikealle.
                                    tr\turning = TRAIN_TURNRIGHT
                                ElseIf checkTrack = TR_TOPRIGHT Then
                                    // Asetetaan juna k‰‰ntym‰‰n vasemmalle.
                                    tr\turning = TRAIN_TURNLEFT
                                ElseIf checkTrack = TR_VERTICAL Or checkTrack = TR_XCROSS
                                    // Menkˆˆn juna suoraan.
                                    tr\turning = TRAIN_NOTURN
                                Else
                                    // Nyt kyll‰ tˆrm‰t‰‰n.
                                    GameOver()
                                    Exit
                                EndIf
                            EndIf // Suunnat ja k‰‰ntymiset
                        EndIf // overTrackTop <> TR_EMPTY
                    EndIf // overTile ei TILE_FINISH eik‰ TILE_START
                    
                    // Juna ei ole en‰‰ immuuni
                    tr\immune = False
                    
                    // Hoidetaan junan liikutuksen logiikka
                    MoveTrain( ConvertToInteger( tr ) )

                Next tr
                
                // Nollataan
                gMovedInTile = gMovement
                centerChecked = False
                
            ElseIf gMovedInTile >= TILESIZE/2 And centerChecked = False Then
                // Jos ollaan kuljettu puoli palasta, eli ollaan keskell‰ palasta.
                centerChecked = True
                
                // Lis‰t‰‰n uusia junia liikenteeseen, jos niit‰ on olemassa.
                PlaceTrains()
                
                For tr.TRAINS = Each TRAINS
                    If tr\immune = False And TileType( tr\tileX, tr\tileY ) = TILE_FINISH Then
                        // Tarkistetaan tˆrm‰ykset loppupalaseen.
                        check = CheckFinishTile( tr\tileX, tr\tileY, tr\trainColor )
                        If check = False Then
                            // Oho, tˆrm‰ttiin, virheilmoitus ja takaisin muokkausmoodiin.
                            GameOver()
                            Exit
                        ElseIf check = True Then
                            // Juna p‰‰si maaliin, poistetaan se pelist‰.
                            DeleteObject tr\obj
                            Delete tr
                        Else
                            // Hoidetaan junan liikutuksen logiikka
                            MoveTrain( ConvertToInteger( tr ) )
                        EndIf
                    Else
                        // Hoidetaan junan liikutuksen logiikka
                        MoveTrain( ConvertToInteger( tr ) )
                    EndIf
                Next tr
                
            Else
                // Hoidetaan kaikkien junien liikutuksen logiikka
                For tr.TRAINS = Each TRAINS
                    MoveTrain( ConvertToInteger( tr ) )
                Next tr
            EndIf
        EndIf
        
        // Enter-n‰pp‰imell‰ laitetaan junat liikkeelle / poistetaan junat.
        If KeyHit( cbKeyReturn ) Or gGameChangeMode Then
            gGameChangeMode = False
            
            // Vaihdetaan muokkauksen salliminen.
            allowEditing = Not allowEditing
            
            If allowEditing Then
                // Jos muokkaus sallitaan, poistetaan olemassa olevat junat kokoelmasta.
                For tr.TRAINS = Each TRAINS
                    DeleteObject tr\obj
                    Delete tr
                Next tr
                
                // Laitetaan kartta alkukuoseihinsa.
                GameResetBlocks()
                
                // Tyhjennet‰n AddTextill‰ luodut debug-tekstit
                If DEBUG Then ClearText
                
                // Nollataan junien liikkeen muuttujat (kts. kommentit niiden m‰‰rittelyst‰).
                gMovement = 0.0
                gMovedInTile = TILESIZE/2
            Else
                // Muulloin asetetaan junat paikoilleen alkupisteisiins‰.
                PlaceTrains()
                
                centerChecked = True
                
                // Asetetaan AddTextit oikeaan kohtaan.
                If DEBUG Then Locate BORDERSIZE, BORDERSIZE*2 + TILESIZE*MAPSIZE + 60
            EndIf
        EndIf
        
        // Piirret‰‰n objektit (junat) raiteiden j‰lkeen ja ennen kartan palasia.
        DrawGame
        
        // Piirret‰‰n kartan palaset.
        DrawBlocks()
        
        If DEBUG Then
            SetFont FONTS( FNT_DEFAULT )
            Color cbWhite
            Text BORDERSIZE, TILESIZE*MAPSIZE + BORDERSIZE*2, "Tile under mouse: " + TileTypeToStr( TileType2( MouseX(), MouseY() ) )
            Text BORDERSIZE, TILESIZE*MAPSIZE + BORDERSIZE*2 + 15, "Top-track under mouse: " + TrackTypeToStr( TrackType2( MouseX(), MouseY() ) )
            Text BORDERSIZE, TILESIZE*MAPSIZE + BORDERSIZE*2 + 30, "Under-track under mouse: " + TrackTypeToStr( TrackType2( MouseX(), MouseY(), True ) )
            Text BORDERSIZE, TILESIZE*MAPSIZE + BORDERSIZE*2 + 45, "Speed: " + speedFactor#
            
            // Nopeuden muutos
            speedFactor# = speedFactor# + MouseMoveZ()*0.01
            
            // Junien tilekoordinaattien listaus
            Color cbGreen
            i=0
            startY = TILESIZE*MAPSIZE + BORDERSIZE*2 + 80
            For tr.TRAINS = Each TRAINS
                Text BORDERSIZE+20, startY + 15*i, "Train "+i+": ( " + tr\tileX + ", " + tr\tileY + ") " + "Turning: " + tr\turning + " | Direction: " + tr\direction
                i = i + 1
            Next tr
        EndIf
        
        // Tarkistetaan pelin h‰vi‰minen.
        If gGameFailed Then 
            DrawOverlayInfo( "You failed!", "Game over" )
            
            // Asetetaan peli taas l‰p‰ist‰v‰ksi
            gGameFailed = False
            
            // Tyhj‰t‰‰n n‰pp‰inpuskuri, ettei peli ala heti uudelleen jos 
            // pelaaja painaa Enteri‰.
            ClearKeys()
        EndIf
        
        // FPS-riippumaton liike
        passedTime = Timer() - oldTime
        oldTime = Timer()
        DrawScreen
    Until EscapeKey() Or KeyHit(cbKeyQ)
    
    // Poistetaan junat kummittelemasta
    For tr.TRAINS = Each TRAINS
        DeleteObject tr\obj
        Delete tr
    Next tr
    
    // Tyhjennet‰‰n nykyinen kartta.
    ClearMap()
    
    If DEBUG Then ClearText
    
    Return True
    
EndFunction

//================================================================================
// Raiteiden piirto karttaan.
//================================================================================
Function GameEditMap()
    // Tarkistetaan hiirell‰ piirto
    blockType = checkRailDraws()
    
    // Otetaan muokkauskoordinaatit talteen mukavampiin muuttujiin.
    tileX = gEditedTileX
    tileY = gEditedTileY
    
    // Mink‰ palasen p‰‰lle yritet‰‰n piirt‰‰
    overTile = TileType( tileX, tileY )
    
    // Jos tapahtui muutos, niin poistetaan muokatun tilen toisarvoinen raide
    // ja asetetaan nykyinen raide toisarvoiseksi. Uusi raite ensiarvoiseksi.
    // MUTTA vain jos uusi block ei ole sama kuin jo nyt p‰‰ll‰ oleva, tai alla
    // oleva palanen on tyhj‰ tahi raide.
    If blockType <> False And ( overTile = TILE_TRACK Or overTile = TILE_EMPTY ) Then
        If blockType <> RAILS( tileX, tileY, RAIL_TOP ) Then
            // Ei muuteta palasia, jos edellinen palanen on jo x-risteys ja uusi palanen olisi suora.
            If Not ( ( blockType = TR_VERTICAL Or blockType = TR_HORIZONTAL ) And RAILS( tileX, tileY, RAIL_TOP ) = TR_XCROSS )
                // Tarkistetaan, tekeekˆ uusi raide vanhan kanssa x-risteyksen.
                If blockType = TR_VERTICAL And RAILS( tileX, tileY, RAIL_TOP ) = TR_HORIZONTAL Then
                    RAILS( tileX, tileY, RAIL_TOP ) = TR_XCROSS
                    RAILS( tileX, tileY, RAIL_UNDER ) = TR_XCROSS
                ElseIf blockType = TR_HORIZONTAL And RAILS( tileX, tileY, RAIL_TOP ) = TR_VERTICAL Then
                    RAILS( tileX, tileY, RAIL_TOP ) = TR_XCROSS
                    RAILS( tileX, tileY, RAIL_UNDER ) = TR_XCROSS
                ElseIf RAILS( tileX, tileY, RAIL_TOP ) = TR_XCROSS Then
                    // Jos vanha raide on X-risteys ja uusi raide on mutka, poistetaan X-risteys ja j‰tet‰‰n vain mutka.
                    RAILS( tileX, tileY, RAIL_TOP ) = blockType
                    RAILS( tileX, tileY, RAIL_UNDER ) = TR_EMPTY
                Else
                    // Muuten vain siirret‰‰n vanha pala alle ja laitetaan uusi palanen p‰‰lle.
                    RAILS( tileX, tileY, RAIL_UNDER ) = RAILS( tileX, tileY, RAIL_TOP )
                    RAILS( tileX, tileY, RAIL_TOP ) = blockType
                EndIf
            EndIf
        EndIf
    EndIf
EndFunction

//================================================================================
// Hieman monimutkainen funktio uuden raiteen selvitt‰miseksi. Uuden palasen
// koordinaatit tallennetaan muuttujiin gEditedTileX ja gEditTileY.
// Funktio tarvitsee avukseen useita globaaleia, koska se on v‰h‰n purkkainen.
// gCRD-alkuiset globaalit ovat funktion sis‰iseen k‰yttˆˆn tarkoitetut.
// Funktiota pit‰‰ kutsua joka loopin kierros.
// -> Funktio palauttaa uuden palasen vakion arvon.
//================================================================================
Global gCRDtileX, gCRDtileY, gCRDstartTileX, gCRDstartTileY
Global gCRDstartX, gCRDstartY, gCRDendX, gCRDendY
Global gEditedTileX, gEditedTileY

Function CheckRailDraws()
    
    gCRDtileX = (MouseX()-BORDERSIZE)/64
    gCRDtileY = (MouseY()-BORDERSIZE)/64
    
    gCRDtileX = Min(Max(gCRDtileX,0),MAPSIZE-1)
    gCRDtileY = Min(Max(gCRDtileY,0),MAPSIZE-1)
    
    If MouseDown(1)
        If gCRDstartTileX=-1 Or gCRDstartTileY=-1 Then gCRDstartTileX=gCRDtileX : gCRDstartTileY=gCRDtileY
        For x2=Max(Min(RoundDown(MouseX()/64)-1,10),0) To Max(Min(RoundDown(MouseX()/64)+1,10),0)
            For y2=Max(Min(RoundDown(MouseY()/64)-1,10),0) To Max(Min(RoundDown(MouseY()/64)+1,10),0)
                For i=0 To 1
                    If GLUEPOINTS(x2,y2,i,0) >= BORDERSIZE And GLUEPOINTS(x2,y2,i,1) >= BORDERSIZE Then
                        If Distance(MouseX(),MouseY(),GLUEPOINTS(x2,y2,i,0),GLUEPOINTS(x2,y2,i,1))<TILESIZE/4 Then 
                            If gCRDstartX=-1 Then
                                gCRDstartX=GLUEPOINTS(x2,y2,i,0)
                                gCRDstartY=GLUEPOINTS(x2,y2,i,1)
                            ElseIf gCRDendX=-1 Or gCRDendX=gCRDstartX
                                gCRDendX=GLUEPOINTS(x2,y2,i,0)
                                gCRDendY=GLUEPOINTS(x2,y2,i,1)
                            EndIf
                        EndIf
                    EndIf
                Next i
            Next y2
        Next x2
        
        If gCRDtileX<>gCRDstartTileX Or gCRDtileY<>gCRDstartTileY Then
            gCRDstartX=-1 : gCRDstartY=-1 : gCRDendX=-1 : gCRDendY=-1 : gCRDstartTileX=-1 : gCRDstartTileY=-1
        EndIf
        
        If gCRDstartX<>-1 And gCRDendX<>-1 Then
            'Color cbYellow
            'Line gCRDstartX,gCRDstartY,gCRDendX,gCRDendY
            angle = GetAngle( gCRDstartX, gCRDstartY, gCRDendX, gCRDendY )
            blockCheck = gCRDstartX - gCRDstartTileX*64 - BORDERSIZE
            If ( gCRDstartX <> gCRDendX ) Or ( gCRDstartY <> gCRDendY ) Then
                block = TR_EMPTY
                If gCRDstartX = gCRDendX Then
                    block = TR_VERTICAL
                ElseIf gCRDstartY = gCRDendY Then
                    block = TR_HORIZONTAL
                ElseIf angle=315 Then
                    If blockCheck=0 Then
                        block = TR_BOTTOMLEFT
                    Else
                        block = TR_TOPRIGHT
                    EndIf
                ElseIf angle=135 Then
                    If blockCheck=32 Then
                        block = TR_BOTTOMLEFT
                    Else
                        block = TR_TOPRIGHT
                    EndIf
                ElseIf angle=225 Then
                    If blockCheck=32 Then
                        block = TR_TOPLEFT
                    Else
                        block = TR_BOTTOMRIGHT
                    EndIf
                ElseIf angle=45 Then
                    If blockCheck=0 Then
                        block = TR_TOPLEFT
                    Else
                        block = TR_BOTTOMRIGHT
                    EndIf
                EndIf
            EndIf
        EndIf
        
    Else 
        gCRDstartX=-1 : gCRDstartY=-1 : gCRDendX=-1 : gCRDendY=-1 : gCRDstartTileX=-1 : gCRDstartTileY=-1
    EndIf
    
    If block<>TR_EMPTY Then
        gEditedTileX = gCRDtileX
        gEditedTileY = gCRDtileY
        Return block
    Else
        Return False
    EndIf
    
EndFunction 

//================================================================================
// Kartan alustus
//================================================================================
Function InitializeMap()
    // Tyhjennet‰‰n muistissa oleva kartta
    ClearMap()
    
    // Alustetaan kaikki etuk‰teen ladatut kuvat tyhjiksi.
    For x=0 To MAPSIZE-1
        For y=0 To MAPSIZE-1
            For i=0 To 9
                If GAME_PRELOADEDSTARTS( x, y, i ) <> 0 Then
                    DeleteImage GAME_PRELOADEDSTARTS( x, y, i )
                EndIf
                GAME_PRELOADEDSTARTS( x, y, i ) = 0
            Next i
        Next y
    Next x
    
    // Rakennetaan testikartta
    // GameAddBlock( _tileX, _tileY, _type, _colors$, _directions )
    GameAddBlock( 2, 2, TILE_START, "0 1 2 3 4 5", 8 )
    GameAddBlock( 6, 2, TILE_FINISH, "5 1 0 3 4 2", 8 )
EndFunction 

//================================================================================
// Raiteiden alustus
//================================================================================
Function InitializeTracks()
    // Asetetaan kaikki raiteet tyhjiksi.
    For x=0 To MAPSIZE-1
        For y=0 To MAPSIZE-1
            RAILS(x,y,RAIL_TOP) = TR_EMPTY
            RAILS(x,y,RAIL_UNDER) = TR_EMPTY
        Next y
    Next x
EndFunction

//================================================================================
// Lis‰‰ palasen pelimaailmaan.
//================================================================================
Function GameAddBlock( _tileX, _tileY, _type, _colors$, _directions )
    newSB.SPECIALBLOCKS = New( SPECIALBLOCKS )
    newSB\tileX = _tileX
    newSB\tileY = _tileY
    newSB\blockType = _type
    newSB\blockColors = _colors$
    newSB\gameColors = newSB\blockColors
    newSB\directions = _directions
    
    // Ladataan etuk‰teen kaikki tulevat kuvat t‰lle palaselle.
    If newSB\blockType = TILE_START Then 
        colors$ = "pois " + newSB\gameColors
        While CountWords( colors$ ) > 0
            colors$ = Trim( Right( colors$, Len( colors$ ) - Len( GetWord( colors$, 1 ) ) ) )
            
            If GAME_PRELOADEDSTARTS( newSB\tileX, newSB\tileY, Int( CountWords( colors$ ) ) ) = 0 Then
                GAME_PRELOADEDSTARTS( newSB\tileX, newSB\tileY, Int( CountWords( colors$ ) ) ) = AssembleBlock( newSB\blockType, colors$, newSB\directions, newSB\blockColors )
            EndIf
        Wend
        
        // Asetetaan ensimm‰inen kuva t‰lle palaselle
        newSB\img = GAME_PRELOADEDSTARTS( newSB\tileX, newSB\tileY, Int( CountWords( newSB\gameColors ) ) )
    EndIf
    
EndFunction

//================================================================================
// Resetoi kaikki pelimaailman palaset alkutilaansa.
//================================================================================
Function GameResetBlocks( )
    For sb.SPECIALBLOCKS = Each SPECIALBLOCKS
        If sb\blockType = TILE_START Then 
            sb\gameColors = sb\blockColors
            sb\img = GAME_PRELOADEDSTARTS( sb\tileX, sb\tileY, Int( CountWords( sb\gameColors ) ) )
            
        ElseIf sb\blockType = TILE_FINISH Then
            sb\gameColors = sb\blockColors
            sb\img = AssembleBlock( sb\blockType, sb\blockColors, sb\directions )
        EndIf
    Next sb
EndFunction

//================================================================================
// Junien asentaminen paikoilleen. Asettaa aina ensimm‰isen v‰rin kohdalle
// junan, ja poistaa sitten alkupalasen tiedoista kyseisen v‰rin. N‰in saadaan
// aikaiseksi se, ett‰ kun t‰t‰ funktiota kutsutaan aina kun junat ovat kulkeneet
// yhden tilen verran eteenp‰in, laitetaan uusi juna maailmaan.
//================================================================================
Function PlaceTrains()
    For sb.SPECIALBLOCKS = Each SPECIALBLOCKS
        If sb\blockType = TILE_START And CountWords( sb\gameColors ) > 0 Then
            // Asetetaan uusi juna samoihin koordinaatteihin kuin alkupalanen,
            // ja k‰‰nnet‰‰n se osoittamaan alkupalasen suuntaa.
            newTrain.TRAINS = New(TRAINS)
            
            // Uuden junan v‰ri on sama kuin palasen ensimm‰inen v‰ri
            newTrain\trainColor = Int( GetWord( sb\gameColors, 1 ) )
            
            // Poistetaan palasen tiedoista juuri lis‰tty v‰ri.
            sb\gameColors = Right( sb\gameColors, Len( sb\gameColors ) - Len( GetWord( sb\gameColors, 1 ) ) )
            sb\gameColors = Trim( sb\gameColors )
            
            // Asetetaan palaselle uusi kuva, koska sielt‰ on nyt poistunut yksi juna.
            sb\img = GAME_PRELOADEDSTARTS( sb\tileX, sb\tileY, Int( CountWords( sb\gameColors ) ) )
            
            // Asetetaan uusi juna immuuniksi tˆrm‰yksille, ettei se tˆrm‰isi alkupalaseen.
            newTrain\immune = True
            
            // Luodaan uusi objekti maailmaan.
            newTrain\obj = CloneObject( TRAINOBJECTS( newTrain\trainColor ) )
            
            // Asetetaan junan tilekoordinaatit samaksi kuin alkupalasen.
            newTrain\tileX = sb\tileX
            newTrain\tileY = sb\tileY
            
            // Asetetaan se keskelle alkupalasta.
            xPos = sb\tileX * TILESIZE + BORDERSIZE + TILESIZE/2
            yPos = sb\tileY * TILESIZE + BORDERSIZE + TILESIZE/2
            ScreenPositionObject newTrain\obj, xPos, yPos
            
            // Asetetaan juna ei-k‰‰ntym‰‰n.
            newTrain\turning = TRAIN_NOTURN
            
            // K‰‰nnet‰‰n juna alkupalasen m‰‰r‰‰m‰‰n suuntaan.
            If sb\directions = 1 Then
                // Ylˆs
                RotateObject newTrain\obj, 90
                newTrain\direction = TRAINDIR_UP
            ElseIf sb\directions = 2 Then
                // Oikealle
                RotateObject newTrain\obj, 0
                newTrain\direction = TRAINDIR_RIGHT
            ElseIf sb\directions = 4 Then
                // Alas
                RotateObject newTrain\obj, 270
                newTrain\direction = TRAINDIR_DOWN
            ElseIf sb\directions = 8 Then
                // Vasemmalle
                RotateObject newTrain\obj, 180
                newTrain\direction = TRAINDIR_LEFT
            Else
                // Palasen suunta ei ollut yksiselitteinen. Poistetaan
                // juna maailmasta.
                DeleteObject newTrain\obj
                Delete newTrain
            EndIf
        EndIf
    Next sb
EndFunction 

//================================================================================
// Tarkistaa, tuliko oikeanv‰rinen juna perille loppupalaseen, ja jos tuli, niin
// v‰hennet‰‰n loppupalasesta kyseinen v‰ri pois. Funktiolle annettavat 
// koordinaatit ovat ruutukoordinaatteja. Funktion eri palautusarvot ovat:
//    1: juna p‰‰si maaliin onnistuneesti
//    0: juna tˆrm‰si (v‰‰r‰n v‰rinen tai maali oli jo t‰ynn‰)
//   -1: juna ei ollut maalin p‰‰ll‰
//================================================================================
Function CheckFinishTile( _tileX, _tileY, _trainColor )

    position = -1
    
    For sb.SPECIALBLOCKS = Each SPECIALBLOCKS
        If sb\tileX = _tileX And sb\tileY = _tileY And sb\blockType = TILE_FINISH Then
            // Ollaan loppupalasen p‰‰ll‰. Tarkistetaan v‰rit.
            For i=1 To CountWords( sb\gameColors )
                position = position + Len( GetWord( sb\gameColors, i ) ) + 1
                If _trainColor = GetWord( sb\gameColors, i ) Then
                    // Maalissa oli oikean v‰rinen palanen. Poistetaan se palasesta.
                    colorsBefore$ = Trim( Left( sb\gameColors, position - Len( GetWord( sb\gameColors, 1 ) ) ) )
                    colorsAfter$ = Trim( Mid( sb\gameColors, position + Len( GetWord( sb\gameColors, 1 ) ) ) )
                    
                    sb\gameColors = colorsBefore$ + " " + colorsAfter$
                    
                    // Asetetaan loppupalaselle uusi kuva.
                    If sb\img <> 0 Then DeleteImage sb\img
                    sb\img = AssembleBlock( TILE_FINISH, sb\gameColors, sb\directions, sb\blockColors )
                    
                    // Palautetaan onnistunut maaliin saapuminen.
                    Return 1
                EndIf
            Next i
            
            // Kaikki v‰rit k‰ytiin l‰pi, mutta oikeaa ei lˆytynyt. Tˆrm‰t‰‰n.
            Return 0
        EndIf
    Next sb
    
    // K‰ytiin koko kokoelma l‰pi, eik‰ palasta lˆytynyt objektin koordinaateista.
    Return -1
EndFunction 

//================================================================================
// Failausilmoitus, kun tˆrm‰t‰‰n johonkin.
//================================================================================
Function GameOver()
    gGameFailed = True
    gGameChangeMode = True
EndFunction 

//================================================================================
// Yhden junan liikutuksen logiikka.
//================================================================================
Function MoveTrain( _typePtr )
    tr.TRAINS = ConvertToType( _typePtr )
    
    If tr\turning = TRAIN_NOTURN Then
        // Menn‰‰n suoraan
        MoveObject tr\obj, gMovement
    ElseIf tr\turning = TRAIN_TURNRIGHT Then
        // K‰‰nnet‰‰n junaa oikealle
        angle# = -( gMovedInTile / TILESIZE ) * 90
        
        If tr\direction = TRAINDIR_UP Then
            angle# = angle# - 270
        ElseIf tr\direction = TRAINDIR_LEFT Then
            angle# = angle# - 180
        ElseIf tr\direction = TRAINDIR_DOWN Then
            angle# = angle# - 90
        EndIf
        
        angle# = WrapAngle( angle# )
        
        RotateObject tr\obj, angle#
        MoveObject tr\obj, gMovement * CURVE_SPEED
    ElseIf tr\turning = TRAIN_TURNLEFT Then
        // K‰‰nnet‰‰n junaa vasemmalle
        angle# = ( gMovedInTile / TILESIZE ) * 90
        
        If tr\direction = TRAINDIR_UP Then
            angle# = angle# - 270
        ElseIf tr\direction = TRAINDIR_LEFT Then
            angle# = angle# - 180
        ElseIf tr\direction = TRAINDIR_DOWN Then
            angle# = angle# - 90
        EndIf
        
        angle# = WrapAngle( angle# )
        
        RotateObject tr\obj, angle#
        MoveObject tr\obj, gMovement * CURVE_SPEED
    EndIf
EndFunction 