//********************************************************************************
// Menu.cb
//
// Valikko
//********************************************************************************

// Vakiot ohjaamaan eri menujen piirtoa.
Const MNU_MAIN       = 0
Const MNU_SETTINGS   = 1
Const MNU_SAVE       = 2
Const MNU_LOAD       = 3

//================================================================================
// Valikon piirt‰minen, p‰‰silmukka.
//================================================================================
Function DrawMenu( _menu = MNU_MAIN )
    
    
    Repeat
        DrawImage GUIIMAGES( GUI_MENUBG ), 0, 0

        SetFont FONTS( FNT_MENU )
        
        If DrawMenuItem( "Start game!", 100 ) Then
            StartGame(CurrentDir()+"saves\DimmingRainbow.coh")
            SetFont FONTS( FNT_MENU )
        ElseIf DrawMenuItem( "Go to editor", 200 ) Then
            Editor()
            SetFont FONTS( FNT_MENU )
        ElseIf DrawMenuItem( "Exit program", 300 ) Then
            Exit
        EndIf
        DrawScreen
    Until EscapeKey() Or KeyHit(cbKeyQ)
EndFunction 


//================================================================================
// Valikon valinnan piirt‰minen
//================================================================================
Function DrawMenuItem( _txt$, _yPosition )
    ret = False
    Color cbWhite
    
    x_coord = ScreenWidth()/2-100
    Text (ScreenWidth()-TextWidth(_txt))/2, _yPosition + 20 - (TextHeight(_txt)/2), _txt
    
    If MouseOver( x_coord, _yPosition, 200, 40 ) Then
        Color cbGreen
        If MouseUp(1) Then
            ret = True
        EndIf
    EndIf
    
    Box x_coord, _yPosition, 200, 40, OFF 
    
    Return ret
EndFunction 

//================================================================================
// Overlay-valikon piirt‰minen. J‰tt‰‰ taakse vanhan ruudun kuvan, ja piirt‰‰
// p‰‰lle valikon pohjan.
// Globaalit gOverlayX, gOverlayY, gOverlayWidth, gOverlayHeight sis‰lt‰v‰t
// tiedon overlayn vasemman yl‰nurkan koordinaateista ja overlayn korkeudesta
// sek‰ leveydest‰.
//================================================================================
Global gOverlayX, gOverlayY, gOverlayWidth, gOverlayHeight
Function DrawOverlayMenu( _title$ = "Menu", _bottomY=-1, _height=-1 )
    
    If _height<0 Then gOverlayHeight = ScreenHeight() - BORDERSIZE*2 - 100 Else gOverlayHeight = _height
    If _bottomY<0 Then gOverlayY = BORDERSIZE + 50 Else gOverlayY = _bottomY - gOverlayHeight

    gOverlayWidth = ScreenWidth() - BORDERSIZE*2 - 60
    gOverlayX = (ScreenWidth() - gOverlayWidth)/2
    
    Color 0, 0, 1
    Box gOverlayX, gOverlayY, gOverlayWidth, gOverlayHeight, ON
    Color cbYellow
    Box gOverlayX, gOverlayY, gOverlayWidth, gOverlayHeight, OFF
    
    SetFont FONTS( FNT_OVERLAYMENU_TITLE )
    Color 128,128,128
    Text gOverlayX+(gOverlayWidth-TextWidth(_title$))/2+2, gOverlayY+6, _title
    Color cbWhite
    Text gOverlayX+(gOverlayWidth-TextWidth(_title$))/2, gOverlayY+4, _title
    
    Color cbGreen
    Line gOverlayX+1, gOverlayY+45, gOverlayX+gOverlayWidth-2, gOverlayY+45
    Line gOverlayX+1, gOverlayY+48, gOverlayX+gOverlayWidth-2, gOverlayY+48
    
    Color cbWhite
    
    bgImg = MakeImage( ScreenWidth(), ScreenHeight() )
    CopyBox 0, 0, ScreenWidth(), ScreenHeight(), 0, 0, SCREEN(), Image(bgImg)
    
    SetFont FONTS( FNT_DEFAULT )
    
    Return bgImg
EndFunction 

//================================================================================
// Ilmoitus-overlay, poistuu itsest‰‰n vakion INFO_DURATION (millisekunneissa) ja
// merkkien m‰‰r‰n m‰‰r‰‰m‰n ajan j‰lkeen, tai jos k‰ytt‰j‰ klikkaa hiirell‰ tai 
// painaa jotain n‰pp‰int‰. Parametrina annetaan info-teksti ja mahdollinen 
// otsikko. Tekstiss‰ pipe-merkki "|" tarkoittaa rivinvaihtoa.
//================================================================================
Const INFO_DURATION = 70 // 0.07 sekuntia per kirjain
Const INFO_MINDURATION = 1500 // V‰hint‰‰n 1.5 sekuntia per viesti
Function DrawOverlayInfo( _txt$, _title$ = "Info", _bottomY=-1 )
    
    SetFont FONTS( FNT_OVERLAYMENU )
    txtHeight = TextHeight("H")
    
    infoWidth = ScreenWidth() - BORDERSIZE*2 - 60
    infoHeight = 70 + ( Int( CountWords( _txt$, "|" ) ) ) * txtHeight
    infoX = (ScreenWidth() - infoWidth)/2
    If _bottomY=-1 Then 
        infoY = BORDERSIZE + 50
    Else
        infoY = _bottomY - infoHeight
    EndIf
    
    Color 120, 217, 255
    Box infoX, infoY, infoWidth, infoHeight, ON
    Color 32, 32, 32
    Box infoX, infoY, infoWidth, infoHeight, OFF
    
    SetFont FONTS( FNT_OVERLAYMENU_TITLE )
    Color 128,128,128
    Text infoX+(infoWidth-TextWidth(_title$))/2+2, infoY+6, _title
    Color cbBlack
    Text infoX+(infoWidth-TextWidth(_title$))/2, infoY+4, _title
    
    Color 32, 32, 32
    Line infoX+1, infoY+45, infoX+infoWidth-2, infoY+45
    Line infoX+1, infoY+48, infoX+infoWidth-2, infoY+48
    
    Color cbBlack
    
    SetFont FONTS( FNT_OVERLAYMENU )
    For i=1 To CountWords( _txt$, "|" )
        Text infoX+20, infoY+60+(i-1)*TextHeight("H"), GetWord( _txt$, i, "|" )
    Next i
    
    bgImg = MakeImage( ScreenWidth(), ScreenHeight() )
    CopyBox 0, 0, ScreenWidth(), ScreenHeight(), 0, 0, SCREEN(), Image(bgImg)
    
    ClearMouse()
    ClearKeys()
    
    loopDuration = Timer() + Max( INFO_MINDURATION, INFO_DURATION * Len( _txt ) )
    Repeat
        DrawImage bgImg, 0, 0
    
        If MouseUp(1) Or MouseUp(2) Or MouseUp(3) Or GetKey() Or loopDuration < Timer() Then
            Exit
        EndIf
        
        DrawScreen
    Forever
    
    Color cbWhite
    Return True
    
EndFunction 